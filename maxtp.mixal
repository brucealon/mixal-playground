***
* the main program
* TODO
*  - After loading the entries from tape, count them
*    from X until a 00000. Set COUNT to the prior index.
*  - In the value, convert leading 30s to 0s, then shift
*    the value over until there is only one leading 0.
*  - In the index, convert leading 30s to 0s, then shift
*    the index over until there is only one leading 0.
***
X           EQU     1000
RES         EQU     2000
* GNU MDK stores tapes at
* ~/.mdk/tapeTAPE.dev
TAPE        EQU     1
TERM        EQU     19
COUNT       EQU     100
* constants for results display
            ORIG    RES+100
VLABEL      ALF     VALUE
ILABEL      ALF     INDEX
* begin
            ORIG    3000
START       IN      X(TAPE)      read the data
            JBUS    *(TAPE)
            ENT1    COUNT        set number of items
            JMP     MAXIMUM      get the maximum
* print the value
            ENT1    RES          print from RES
            MOVE    VLABEL       set the label
            ENT6    0
            JMP     OUTPUT
* print the index
            ENT1    RES
            MOVE    ILABEL
            ST2     RES+1        get the index of the results
            LDA     RES+1
            ENT6    0
            JMP     OUTPUT
            HLT
***
* the output subroutine
* r6 indexes from RES, usually 0
* rA contains a number to print (converted by CHAR)
* so RES,6 should have the label
*    RES+1,6 should end up with the number
***
OUTPUT      STJ     1F
            CHAR
            STX     RES+1,6
            OUT     RES,6(TERM)
            IOC     0(TERM)
1H          JMP     *
***
* the max subroutine
***
MAXIMUM     STJ     EXIT
INIT        ENT3    0,1
            JMP     CHANGEM
LOOP        CMPA    X,3
            JGE     *+3
CHANGEM     ENT2    0,3
            LDA     X,3
            DEC3    1
            J3P     LOOP
EXIT        JMP     *
            END     START
